/* REXX */
RC=ISFCALLS('ON')
     /* ACCESS THE ST DISPLAY */
ADDRESS SDSF "ISFEXEC ST"
LRC=RC
CALL MSGRTN
IF LRC<>0 THEN
  EXIT 20
     /* LOOP FOR ALL RUNNING RJONES JOBS */
DO IX=1 TO JNAME.0
  IF JNAME.IX = "CICSTS56" & ,
     QUEUE.IX = "EXECUTION" & ,
     ACTSYS.IX <> "" THEN
    DO
    /* ISSUE THE ? (JDS) ACTION AGAINST THE  */
    /* ROW TO LIST THE DATA SETS IN THE JOB. */
      ADDRESS SDSF "ISFACT ST TOKEN('"TOKEN.IX"') PARM(NP ?)" ,
         "( PREFIX JDS_"
      LRC=RC
      CALL MSGRTN
      IF LRC<>0 THEN
        EXIT 20
    /* FIND THE JESYSMSG DATA SET AND ALLOCATE IT */
    /* USING THE SA ACTION CHARACTER              */
      DO JX=1 TO JDS_DDNAME.0
        IF JDS_DDNAME.JX = "JESYSMSG" THEN
          DO
            ADDRESS SDSF "ISFACT ST TOKEN('"JDS_TOKEN.JX"')" ,
              "PARM(NP SA)"
            LRC=RC
            CALL MSGRTN
            IF LRC<>0 THEN
              EXIT 20
    /* READ THE RECORDS FROM THE DATA SET AND LIST THEM. */
    /* THE DDNAME FOR EACH ALLOCATED DATA SET WILL BE IN */
    /* THE ISFDDNAME STEM.  SINCE THE SA ACTION WAS DONE */
    /* FROM JDS, ONLY ONE DATA SET WILL BE ALLOCATED.    */
            DO KX=1 TO ISFDDNAME.0
              SAY "NOW READING" ISFDSNAME.KX
              "EXECIO * DISKR" ISFDDNAME.KX "(STEM LINE. FINIS"
              SAY "  LINES READ:" LINE.0
              DO LX = 1 TO LINE.0
                SAY "  LINE."LX "IS:" LINE.LX
              END
            END
          END
      END
    END
 END
  RC=ISFCALLS('OFF')
  EXIT
    /* SUBROUTINE TO LIST ERROR MESSAGES */
 MSGRTN: PROCEDURE EXPOSE ISFMSG ISFMSG2.
    /* THE ISFMSG VARIABLE CONTAINS A SHORT MESSAGE */
 IF ISFMSG<>"" THEN
   SAY "ISFMSG IS:" ISFMSG
    /* THE ISFMSG2 STEM CONTAINS ADDITIONAL DESCRIPTIVE */
    /* ERROR MESSAGES                                   */
  DO IX=1 TO ISFMSG2.0
   SAY "ISFMSG2."IX "IS:" ISFMSG2.IX
 END
 RETURN
